plugins {
	id("java")
	id("eclipse")
	id("org.spongepowered.gradle.plugin") version "2.1.1"
}

group = "sawfowl.clearlag"
version = "${major}.${minor}-${api}-${suffix}"

java {
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}

repositories {
	mavenCentral()
	maven {
		name = "Sponge"
		url = "https://repo.spongepowered.org/repository/maven-public"
	}
	maven {
		name = "JitPack"
		url 'https://jitpack.io'
	}
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

dependencies {
	implementation "org.spongepowered:spongeapi:11.0.0-SNAPSHOT"
	implementation "com.github.SawFowl:LocaleAPI:3.0"
	implementation 'com.github.SawFowl:CommandPack:2.0'
}

sponge {
	apiVersion("11.0.0-SNAPSHOT")
	loader {
		name("java_plain")
		version("1.0")
	}
	license("GPL-3.0")
	plugin("clearlag") {
		displayName(project.name.capitalize())
		version("${project.version}")
		entrypoint("${project.group}.${rootProject.name}")
		description("Plugin for optimizing your Minecraft server.")
		contributor("SawFowl") {
			description("Author")
		}
		dependency("spongeapi") {
			optional(false)
			loadOrder("after")
			version("11.0.0")
		}
		dependency("localeapi") {
			optional(false)
			loadOrder("after")
			version("3.0.0")
		}
		dependency("commandpack") {
			optional(false)
			loadOrder("after")
			version("2.0.0")
		}
	}
}
